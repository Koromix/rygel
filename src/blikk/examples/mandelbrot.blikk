let y_min = -0.2
let y_max = 0.1
let x_min = -1.5
let x_max = -1.1

let mut characters: [10] String
characters[0] := " "
characters[1] := "."
characters[2] := ":"
characters[3] := ";"
characters[4] := "+"
characters[5] := "="
characters[6] := "x"
characters[7] := "X"
characters[8] := "$"
characters[9] := "&"

for i in 0:24
    for j in 0:80
        let x = (toFloat(j) / 79.0) * (x_max - x_min) + x_min
        let y = (toFloat(i) / 24.0) * (y_max - y_min) + y_min

        let mut pixel = " "
        let mut x0 = x
        let mut y0 = y

        for iter in 0:80
            let x1 = (x0 * x0) - (y0 * y0) + x
            let y1 = 2.0 * x0 * y0 + y

            x0 := x1
            y0 := y1

            # Stop if the point escaped.
            let d = (x0 * x0) + (y0 * y0)
            if d > 4.0
                pixel := characters[iter / 8]
                break
            end
        end

        print(pixel)
    end

    printLn("")
end
